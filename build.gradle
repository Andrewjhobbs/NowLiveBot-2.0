plugins {
    id 'java'
    id 'application'
    id 'eclipse'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

mainClassName = 'core.Main'

def appName = 'NowLive'
group 'com.nowlivebot'
version '0.1-VS-PreRelease'

task copyExtra {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
    }
    outputs.file new File("$buildDir/Dockerfile")
    doLast {
        copy {
            from "Dockerfile"
            into "$buildDir"
        }
    }
}

applicationDistribution.from(copyExtra) {
    into ""
}

task fatJar(type: Jar) {
    baseName rootProject.name
    manifest {
        attributes('Implementation-Title': appName, 'Implementation-Version': version)
        attributes('Main-Class': mainClassName)
    }
    classifier = 'all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
            {
                exclude "META-INF/*.SF"
                exclude "META-INF/*.DSA"
                exclude "META-INF/*.RSA"
            }
    with jar
}

artifacts {
    archives fatJar
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven {
        name "beam"
        url "https://maven.beam.pro/content/repositories/snapshots"
    }
}
dependencies {
    compile group: 'net.dv8tion', name: 'JDA', version: '3.0.BETA_95'
    compile group: 'com.github.avluis', name: 'Java-Twitch-Api-Wrapper', version: '0.3.1'
    compile group: 'pro.beam', name: 'api', version: '3.0.0-SNAPSHOT'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.5'
    compile group: 'com.mchange', name: 'c3p0', version: '0.9.5.2'
    compile group: 'com.mchange', name: 'mchange-commons-java', version: '0.2.12'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
